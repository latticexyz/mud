import { CollapseCode } from "../../components/CollapseCode";

# Balance

ETH is stored in the `World` contract and owned by namespaces.
Any ETH sent with a call to a `System` is kept on the `World`, and credited to the namescape to which the `System` belongs.
This has two important effects:

- The ETH balance is shared between the different `System` contracts in the same namespace of the same `World`.
- The same `System` can be used in multiple `World` contracts, and in multiple namespaces in the same `World`, and the ETH balances remain separate.

To know how much ETH a call has included a `System` can check [`_msgValue()`](https://github.com/latticexyz/mud/blob/main/packages/world/src/WorldContext.sol#L34-L36).

To know how much ETH a specific namespace has you can look in `world:Balances` (the `Balances` table of the `world` namespace).

<details>

<summary>See this in action</summary>

1. Have a MUD application running.
   The easiest way to do this is to [run the template locally](/template/typescript/getting-started).

1. Here we are not concerned with the client, so change to the `contracts` page.
   Because we are not concerned with the client, all the file names will be relative to `.../packages/contracts`.

   ```sh copy
   cd packages/contracts
   ```

1. Create a `.env` file with:

   - `PRIVATE_KEY` - the private key of an account that has ETH on the blockchain.
   - `WORLD_ADDRESS` - the address of the `World` to which you add the namespace.

   If you are using the template with a fresh `pnpm dev`, then you can use this `.env`:

   ```sh filename=".env" copy
   # Anvil default private key for the second account
   # (NOT the account that deployed the World)
   PRIVATE_KEY=0x59c6995e998f97a5a0044966f0945389dc9e86dae88c7a8412f4603b6b78690d

   # Address for the world we are extending
   WORLD_ADDRESS=0x6e9474e9c83676b9a71133ff96db43e7aa0a4342
   ```

1. Create this file as `scripts/GetBalance.s.sol`.

   ```solidity filename="GetBalance.s.sol" copy
   // SPDX-License-Identifier: MIT
   pragma solidity >=0.8.21;

   import { Script } from "forge-std/Script.sol";
   import { console } from "forge-std/console.sol";
   import { Balances } from "@latticexyz/world/src/codegen/tables/Balances.sol";
   import { StoreSwitch } from "@latticexyz/store/src/StoreSwitch.sol";
   import { ResourceId } from "@latticexyz/store/src/ResourceId.sol";
   import { WorldResourceIdLib } from "@latticexyz/world/src/WorldResourceId.sol";

   contract GetBalance is Script {
     function run() external {
       address worldAddress = vm.envAddress("WORLD_ADDRESS");
       StoreSwitch.setStoreAddress(worldAddress);
       console.log("World at:", worldAddress);
       ResourceId nsResource = WorldResourceIdLib.encodeNamespace(bytes14(""));
       console.log("Namespace ID: %x", uint256(ResourceId.unwrap(nsResource)));
       uint256 balance = Balances.get(nsResource);
       console.log("Balance: %d wei", balance);
     }
   }
   ```

   <details>

   <summary>Explanation</summary>

   ```solidity
   // SPDX-License-Identifier: MIT
   pragma solidity >=0.8.21;
   ```

   Standard Solidity boilerplate

   ```solidity
   import { Script } from "forge-std/Script.sol";
   import { console } from "forge-std/console.sol";
   ```

   Standard [`forge` script](https://book.getfoundry.sh/tutorials/solidity-scripting) boilerplate.

   ```solidity
   import { Balances } from "@latticexyz/world/src/codegen/tables/Balances.sol";
   ```

   The `Balances` table contains namespace balances.
   Note that while this table's Solidity code is available as part of the library, it is a standard MUD table and as such gets created from [a `mud.config.ts` file](https://github.com/latticexyz/mud/blob/main/packages/world/mud.config.ts#L55-L62).

   ```solidity
   import { StoreSwitch } from "@latticexyz/store/src/StoreSwitch.sol";
   ```

   We need [this file](https://github.com/latticexyz/mud/blob/main/packages/store/src/StoreSwitch.sol) to specify the `World`'s address.

   ```solidity
   import { ResourceId } from "@latticexyz/store/src/ResourceId.sol";
   import { WorldResourceIdLib } from "@latticexyz/world/src/WorldResourceId.sol";
   ```

   We need the namespace's `ResourceId`.

   ```solidity
   contract GetBalance is Script {
       function run() external {
           address worldAddress = vm.envAddress("WORLD_ADDRESS");
   ```

   Get `WORLD_ADDRESS` from `.env`.

   ```solidity
          StoreSwitch.setStoreAddress(worldAddress);
          console.log("World at:", worldAddress);
   ```

   [Set the address of the `World`](https://github.com/latticexyz/mud/blob/main/packages/store/src/StoreSwitch.sol#L58-L65) so future data calls to go to the correct location.

   ```solidity
          ResourceId nsResource = WorldResourceIdLib.encodeNamespace(bytes14(""));
          console.log("Namespace ID: %x", uint256(ResourceId.unwrap(nsResource)));
   ```

   Create the `ResourceId` for the root namespace.

   ```solidity
          uint256 balance = Balances.get(nsResource);
          console.log("Balance: %d wei", balance);
      }
   }
   ```

   Finally, get the balance from the `world:Balances` table.

   </details>

1. Source the `.env` file (we are going to use the variables there to transfer ETH to the root namespace's account).

   ```sh copy
   source .env
   ```

1. Run the script.

   ```sh copy
   forge script script/GetBalance.s.sol --rpc-url http://localhost:8545
   ```

   The balance should be zero for now.

1. To allow `increment` to accept wei, edit `src/systems/IncrementSystem.sol` to make it `payable`:

   <CollapseCode>

   ```solidity filename="IncrementSystem.sol" showLineNumbers copy {8}
   // SPDX-License-Identifier: MIT
   pragma solidity >=0.8.21;

   import { System } from "@latticexyz/world/src/System.sol";
   import { Counter } from "../codegen/index.sol";

   contract IncrementSystem is System {
     function increment() public payable returns (uint32) {
       uint32 counter = Counter.get();
       uint32 newValue = counter + 1;
       Counter.set(newValue);
       return newValue;
     }
   }
   ```

   </CollapseCode>

1. `pnpm dev` deploys the new code, but it deploys it to a new `World`.
   Restart `pnpm dev` to have a `World` in the same address as before.

1. Call `increment` with ETH.

   ```sh copy
   cast send --private-key $PRIVATE_KEY 0x6E9474e9c83676B9A71133FF96Db43E7AA0a4342 --value 1ether "increment()"
   ```

1. Run the script again to see that the balance is higher.

   ```sh copy
   forge script script/GetBalance.s.sol --rpc-url http://localhost:8545
   ```

</details>

To transfer ETH out of the `World` you need to have the `access` permission level to the namespace itself.
Note that `System` contracts within the namespace do have that permission.

You use either [`transferBalanceToNamespace`](https://github.com/latticexyz/mud/blob/main/packages/world/src/modules/core/implementations/BalanceTransferSystem.sol#L23-L52), if you want to transfer between namespaces in the same `World`, or [`transferBalanceToAddress`](https://github.com/latticexyz/mud/blob/main/packages/world/src/modules/core/implementations/BalanceTransferSystem.sol#L54-L77), to transfer to a different address.

<details>

<summary>See this in action</summary>

These steps assume you already transferred 1 ether to the root namespace.

1. Run this command to transfer 10 wei to the zero address.

   ```sh copy
   cast send --private-key $PRIVATE_KEY 0x6E9474e9c83676B9A71133FF96Db43E7AA0a4342 "transferBalanceToAddress(bytes32,address,uint256)" 0x6e73000000000000000000000000000000000000000000000000000000000000 `cast address-zero` 10
   ```

1. See that the namespace's balance is lower by 10 wei.

   ```sh copy
   forge script script/GetBalance.s.sol --rpc-url http://localhost:8545
   ```

1. See that 10 wei have been deducted from the balance of the `World`.

   ```sh copy
   cast balance $WORLD_ADDRESS
   ```

</details>
