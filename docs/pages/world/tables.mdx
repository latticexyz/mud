import { CollapseCode } from "../../components/CollapseCode";
import TableInWorld from "../../components/common-text/TableInWorld.mdx";
import TableImmutable from "../../components/common-text/TableImmutable.mdx";

# Tables

`World` uses the [store tables](/store/tables), but adds [access control](/world/namespaces-access-control).
For onchain tables, the data is stored by the `World` contract, [which is also a `StoreData`](https://github.com/latticexyz/mud/blob/main/packages/world/src/World.sol#L43).

When a `System` reads or writes storage via [table libraries](/store/table-libraries), the request goes into [`StoreSwitch`](https://github.com/latticexyz/mud/blob/main/packages/store/src/StoreSwitch.sol). This library decides which approach to use:

- If the `System` is in the root namespace, then it was called with [`delegatecall`](https://docs.soliditylang.org/en/v0.8.22/introduction-to-smart-contracts.html#delegatecall-and-libraries).
  This means it inherits the `World` storage and can write directly to storage using [`StoreCore`](https://github.com/latticexyz/mud/blob/main/packages/store/src/StoreCore.sol).
  These calls bypass access control.

- If the `System` is in any other namespace, then it was called with [`call`](https://docs.soliditylang.org/en/v0.8.22/introduction-to-smart-contracts.html#message-calls) and has to call back into the `World` using [`IStore`](https://github.com/latticexyz/mud/blob/main/packages/store/src/IStore.sol).
  These calls go through access control.
  They are only permitted if the `System` has access to the table in question.
  By default a `System` has access to its own namespace and therefore to all the tables inside it.
  Additional [access can be granted](./namespaces-access-control) by the namespace owner.

<TableInWorld />

<TableImmutable />

## Code samples

### Reading from a table

Anybody connected to the blockchain can run the `view` functions that read table content, provided they know which key to use (by default MUD does not keep a list of keys written to a table onchain, to save on storage operations).

All the functions to [read from a MUD store](/store/table-libraries#reading-data) are available.
In this example we use the `Counter` table in the [vanilla](../templates/typescript/vanilla) template, which is a singleton so there is no key to worry about.

<CollapseCode>

```solidity filename="ReadCounter.s.sol" copy showLineNumbers {6-7,11-13}
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.24;

import { Script } from "forge-std/Script.sol";
import { console } from "forge-std/console.sol";
import { StoreSwitch } from "@latticexyz/store/src/StoreSwitch.sol";
import { Counter } from "../src/codegen/index.sol";

contract ReadCounter is Script {
  function run() external {
    address worldAddress = 0xC14fBdb7808D9e2a37c1a45b635C8C3fF64a1cc1;
    StoreSwitch.setStoreAddress(worldAddress);
    console.log("Counter value:", Counter.get());
  }
}
```

</CollapseCode>

<details>

<summary>Explanation</summary>

```solidity
import { StoreSwitch } from "@latticexyz/store/src/StoreSwitch.sol";
```

We need [the `StoreSwitch` library](https://github.com/latticexyz/mud/blob/main/packages/store/src/StoreSwitch.sol) library to specify the address of the `World` with the data.

```solidity
import { Counter } from "../src/codegen/index.sol";
```

It is easiest if we import the definitions of the table that were generated using [`mud tablegen`](/cli/tablegen).

```solidity
    address worldAddress = 0xC14fBdb7808D9e2a37c1a45b635C8C3fF64a1cc1;
    StoreSwitch.setStoreAddress(worldAddress);
```

Use [`StoreSwitch`](https://github.com/latticexyz/mud/blob/main/packages/store/src/StoreSwitch.sol) with the `World` address.

```solidity
    console.log("Counter value:", Counter.get());
```

Read the information.
If this had been a table with a key, we'd need to provide the key as a parameter to `<table name>.get()`.

</details>

### Writing to table

All the functions to [write to a MUD store](/store/table-libraries#writing-data) are available.
In this example we reset `Counter` to zero.
Note that only [authorized addresses](/world/namespaces-access-control#access) are allowed to write directly to a table.

<CollapseCode>

```solidity filename="ResetCounter.s.sol" copy showLineNumbers {20}
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.24;

import { Script } from "forge-std/Script.sol";
import { console } from "forge-std/console.sol";
import { StoreSwitch } from "@latticexyz/store/src/StoreSwitch.sol";
import { Counter } from "../src/codegen/index.sol";

contract ResetCounter is Script {
  function run() external {
    // Specify a store so that you can use tables directly
    address worldAddress = 0xC14fBdb7808D9e2a37c1a45b635C8C3fF64a1cc1;
    StoreSwitch.setStoreAddress(worldAddress);

    // Load the private key from the `PRIVATE_KEY` environment variable (in .env)
    uint256 deployerPrivateKey = vm.envUint("PRIVATE_KEY");

    // Start broadcasting transactions from the deployer account
    vm.startBroadcast(deployerPrivateKey);
    Counter.set(0);
    vm.stopBroadcast();

    console.log("Counter value:", Counter.get());
  }
}
```

</CollapseCode>

<details>

<summary>Explanation</summary>

```solidity
    Counter.set(0);
```

This is how you modify a table's value. If there was a key, it would be `<table name>.set(<key fields>,<value fields>)`.

</details>
